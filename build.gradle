// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    versions = [
            buildTools : '23.0.2',
            compileSdk : 23,
            minSdk     : 16,
            targetSdk  : 23,
            supportLib : '23.1.1',
            playService: '8.3.0',
            retrofit   : '2.0.0-beta2',
            rxandroid  : '1.0.1',
            rx         : '1.0.16',
    ]

    Properties properties = new Properties()
    def localProp = project.rootProject.file('local.properties')
    if (!localProp.exists()) {
        throw new RuntimeException("set local.properties");
    }
    properties.load(localProp.newDataInputStream())
    def apiKey = properties.getProperty('openweathermap.apiKey')
    if (apiKey == null) {
        throw new RuntimeException("need OpenWeatherMap ApiKey");
    }
    env = [
            sdkDir              : properties.getProperty('sdk.dir') ?: System.getenv("ANDROID_HOME"),
            j8Home              : properties.getProperty('java8_home') ?: System.getenv("JAVA8_HOME"),
            j7Home              : properties.getProperty('java7_home') ?: System.getenv("JAVA7_HOME"),
            openWeatherMapApiKey: apiKey,
    ]
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {

    repositories {
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }


    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all,-serial,-varargs,-processing"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}
